/**
 * Build setup
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.flywaydb.flyway'

repositories {
    jcenter()
}

/**
 * Compile setup
 */
dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile(
            [group: 'com.airhacks', name: 'afterburner.fx', version: '1.6.2'],
            [group: 'javax.inject', name: 'javax.inject', version: '1'],

            [group: 'com.orientechnologies', name:'orientdb-core', version:'2.1.6'],
            [group: 'com.orientechnologies', name:'orientdb-object', version:'2.1.6'],

            [group: 'com.offbytwo', name: 'docopt', version: '0.6.0.20150202'],
            [group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'],
            [group: 'org.projectlombok', name: 'lombok', version: '1.16.6'],

            [group: 'org.controlsfx', name: 'controlsfx', version : '8.0.5']
    )

    testCompile(
            [group: 'junit', name: 'junit', version: '4.12']
    )
}

sourceSets {
    main {
        resources.srcDirs = [
            "src/main/java",
            "src/main/resources"
        ]
    }
}

mainClassName = 'headhunt.wizard.Wizard'
version = '1.0.0'

/**
 * Migration
 */
flyway {
    url = null
    locations = null
}

/**
 * Run configuration
 */
run {
    if (project.hasProperty('ENV')) {
        systemProperties['ENV'] = getProperty('ENV')
    } else {
        systemProperties['ENV'] = 'development'
    }

    println "ENV: ${systemProperties['ENV']}"
}

/**
 * FAT-JAR
 */
task fatJar(type: Jar) {

    libsDirName = 'jar'

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    manifest {
        attributes 'Implementation-Title': 'Headhunt application'
        attributes 'Implementation-Version': '0.0.1'
        attributes 'Main-Class': 'headhunt.Main'
    }
}
