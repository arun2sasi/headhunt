/**
 * Build setup
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.0.0-b10'
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1'
    }
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'org.flywaydb.flyway'

repositories {
    jcenter()
}

/**
 * Compile setup
 */
dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile(
            [group: 'com.airhacks', name: 'afterburner.fx', version: '1.6.2'],
            [group: 'javax.inject', name: 'javax.inject', version: '1'],

            [group: 'com.orientechnologies', name:'orientdb-core', version:'2.1.6'],
            [group: 'com.orientechnologies', name:'orientdb-object', version:'2.1.6'],

            [group: 'com.offbytwo', name: 'docopt', version: '0.6.0.20150202'],
            [group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'],
            [group: 'org.projectlombok', name: 'lombok', version: '1.16.6'],
    )

    testCompile(
            [group: 'junit', name: 'junit', version: '4.12']
    )
}

sourceSets {
    main {
        resources.srcDirs = [
            "src/main/java",
            "src/main/resources"
        ]
    }
}

mainClassName = 'com.urosjarc.headhunt.App'

/**
 * Porting javafx
 */
jfxmobile {
    android {
        manifest = 'src/android/AndroidManifest.xml'
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
    }
}

/**
 * Migration
 */
flyway {
    url = null
    locations = null
}

/**
 * Tests
 */
test {
    testLogging.showStandardStreams = true

    minHeapSize = "128m"
    maxHeapSize = "512m"

    jvmArgs '-XX:MaxPermSize=256m'

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message)
    }
}

/**
 * Run configuration
 */
run {
    if (project.hasProperty('ENV')) {
        systemProperties['ENV'] = getProperty('ENV')
    } else {
        systemProperties['ENV'] = 'development'
    }

    println "ENV: ${systemProperties['ENV']}"
}
